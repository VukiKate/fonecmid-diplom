#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область СлужебныеПроцедурыИФункции

Функция СоздатьДокументыРту(ПередаваемыйПараметр) Экспорт 
	
	Если ТипЗнч(ПередаваемыйПараметр) = Тип("Структура") Тогда  
		Период = ПередаваемыйПараметр.Период;
	Иначе 
		Период = ПередаваемыйПараметр;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДоговорыКонтрагентов.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВТ_Договора
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|ГДЕ
	|	НЕ ДоговорыКонтрагентов.ПометкаУдаления
	|	И ДоговорыКонтрагентов.ВидДоговора = &ВидДоговора
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Договора.Ссылка КАК Договор,
	|	ДоговорыКонтрагентов.Владелец КАК Контрагент,
	|	ДоговорыКонтрагентов.Организация КАК Организация
	|ИЗ
	|	ВТ_Договора КАК ВТ_Договора
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|		ПО ВТ_Договора.Ссылка = РеализацияТоваровУслуг.Договор
	|			И (РеализацияТоваровУслуг.Проведен = ИСТИНА)
	|			И (НАЧАЛОПЕРИОДА(РеализацияТоваровУслуг.Дата, МЕСЯЦ) = НАЧАЛОПЕРИОДА(&ПараметрПериод, МЕСЯЦ))
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|		ПО ВТ_Договора.Ссылка = ДоговорыКонтрагентов.Ссылка
	|ГДЕ
	|	РеализацияТоваровУслуг.Ссылка ЕСТЬ NULL
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Договора.Ссылка,
	|	ДоговорыКонтрагентов.Владелец,
	|	ДоговорыКонтрагентов.Организация";
	
	Запрос.УстановитьПараметр("ВидДоговора", Перечисления.ВидыДоговоровКонтрагентов.ВКМ_АбонентскоеОбслуживание);
	Запрос.УстановитьПараметр("ПараметрПериод", Период);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	МассивДокументов = Новый Массив;
	
	Пока Выборка.Следующий() Цикл
		НоваяРеализация = Документы.РеализацияТоваровУслуг.СоздатьДокумент(); 
		НоваяРеализация.Дата = Период;
		НоваяРеализация.Ответственный = Пользователи.ТекущийПользователь(); 
		НоваяРеализация.Договор = Выборка.Договор;
		НоваяРеализация.Контрагент = Выборка.Контрагент; 
		НоваяРеализация.Организация = Выборка.Организация;
		НоваяРеализация.Заполнить(МассивДокументов);
		НоваяРеализация.ВКМ_ВыполнитьАвтозаполнение();
		НоваяРеализация.Записать(РежимЗаписиДокумента.Проведение,РежимПроведенияДокумента.Неоперативный); 
		МассивДокументов.Добавить(НоваяРеализация.Ссылка)
	КонецЦикла;
	
	Возврат МассивДокументов;
	
КонецФункции

#КонецОбласти

#КонецЕсли


